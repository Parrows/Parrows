\section{Parallel Arrows}
\label{sec:parallel-arrows}
Arrows are a general interface to computation. Here we introduce special Arrows as general interface to \textit{parallel computations}. First, we present the interface and explain the reasonings behind it. Then, we discuss some implementations using existing parallel Haskells. Finally, we explain why using Arrows for expressing parallelism is beneficial.
\subsection{The ArrowParallel type class}
A~parallel computation (on functions) in its purest form can be seen as execution of some functions |a -> b| in parallel, as our |parEvalN| prototype shows (Sec.~\ref{sec:parEvalNIntro}).
Translating this into arrow terms gives us a new operator |parEvalN| that lifts a list of arrows |[arr a b]| to a parallel arrow |arr [a] [b]|. % (Fig.~\ref{fig:parEvalNArrowFn}) (
This combinator is similar to our utility function |evalN| from Appendix~\ref{utilfns}, but does parallel instead of serial evaluation.
% \begin{figure}[h]
\begin{code}
parEvalN :: (Arrow arr) => [arr a b] -> arr [a] [b]
\end{code}
% \caption{parEvalN Arrow combinator as a function}
% \label{fig:parEvalNArrowFn}
% \end{figure}
With this definition of |parEvalN|, parallel execution is yet another arrow combinator. But as the implementation may differ depending on the actual type of the arrow |arr| and we want this to be an interface for different backends, we introduce a new type class |ArrowParallel arr a b|: %(Fig.~\ref{fig:parEvalNArrowTypeClass1}).
% \begin{figure}[h]
\begin{code}
class Arrow arr => ArrowParallel arr a b where
	parEvalN :: [arr a b] -> arr [a] [b]
\end{code}
% \caption{parEvalN Arrow combinator in a first version of the ArrowParallel typeclass}
% \label{fig:parEvalNArrowTypeClass1}
% \end{figure}
Sometimes parallel Haskells require or allow for additional configuration parameters, \eg an information about the execution environment or the level of evaluation (weak head normal form vs. normal form). For this reason we also introduce an additional |conf| parameter to the function. We also do not want |conf| to be a fixed type, as the configuration parameters can differ for different instances of |ArrowParallel|. % So we add it to the type signature of the typeclass as well: %(Fig.~\ref{fig:parEvalNArrowTypeClassFinal}).
% % \begin{figure}[h]
% \olcomment{|ArrowParallel arr a b conf| or |ArrowParallel conf arr a b|?} \mbcomment{does it really matter?}
\begin{code}
class Arrow arr => ArrowParallel arr a b conf where
	parEvalN :: conf -> [arr a b] -> arr [a] [b]
\end{code}
% \caption{parEvalN Arrow combinator in the final version of the ArrowParallel typeclass}
% \label{fig:parEvalNArrowTypeClassFinal}
% \end{figure}
% , as we will see in the implementation of the GpH Haskell and the |Par| Monad backends.

\subsection{ArrowParallel instances}

\subsubsection{Glasgow parallel Haskell} \label{sec:parrows:multicore}
The GpH implementation of |ArrowParallel| is implemented in a straightforward manner, but a bit different compared to the variant from Section \ref{sec:GpHIntro}, by using |evalN :: [arr a b] -> arr [a] [b]| (definition in Appendix~\ref{utilfns}, think |zipWith ($)| on Arrows) combined with |withStrategy :: Strategy a -> a -> a| from GpH, where |withStrategy| is the same as |using :: a -> Strategy a -> a|, but with flipped parameters. Our |Conf a| datatype simply wraps a |Strategy a|, but could be extended in future versions of our DSL.
\begin{figure}[t]
\begin{code}
data Conf a = Conf (Strategy a)

instance (NFData b, ArrowChoice arr) =>
  ArrowParallel arr a b (Conf b) where
    parEvalN (Conf strat) fs =
        evalN fs >>>
        arr (withStrategy (parList strat))
\end{code}% $ %% formatting
\caption{The |ArrowParallel| instance for the GpH backend.}
\label{fig:ArrowParallelMulticoreConfigurable}
\end{figure}

\subsubsection{|Par| Monad}
\olcomment{introduce a newcommand for par-monad, "arrows", "parrows" and replace all mentions to them to ensure uniform typesetting \done, we write Arrows. also "Monad"? \done}
Just like for GpH we can easily lift the definition of |parEvalN| to Arrows. To start off, we define the |Strategy a| and |Conf a| type so we can have a configurable instance of ArrowParallel:
\begin{code}
type Strategy a = a -> Par (IVar a)
data Conf a = Conf (Strategy a)
\end{code}
Now we can once again define our |ArrowParallel| instance as follows: First, we convert our Arrows |[arr a b]| with |evalN (map (>>> arr strat) fs)| into an Arrow |arr [a] [(Par (IVar b))]| that yields composable computations in the |Par| monad, where |evalN| is once again of the type |evalN :: [arr a b] -> arr [a] [b]|. By combining the result of this Arrow with |arr sequenceA|, we get an Arrow |arr [a] (Par [IVar b])|. Then, in order to fetch the results of the different threads, we map over the |IVar|s inside the |Par| Monad with |arr (>>= mapM get)| - our intermediary Arrow is of type |arr [a] (Par [b])|. Finally, we execute the comptuation |Par [b]| by composing with |arr runPar| and get the final Arrow |arr [a] [b]|.
\begin{figure}[h]
\begin{code}
instance (NFData b, ArrowChoice arr) => ArrowParallel arr a b (Conf b) where
    parEvalN (Conf strat) fs = evalN (map (>>> arr strat) fs) >>>
                    arr sequenceA >>>
                    arr (>>= mapM Control.Monad.Par.get) >>>
                    arr runPar
\end{code} %$ %% formatting
\caption{ArrowParallel instance for the Par Monad backend.}
\label{fig:ArrowParallelParMonad}
\end{figure}

\subsubsection{Eden}
For both the GpH Haskell and |Par| Monad implementations we could use general instances of |ArrowParallel| that just require the |ArrowChoice| type class. With Eden this is not the case as we can only spawn a list of functions, which we cannot extract from general arrows. While we could still manage to have only one instance in the module by introducing a type class % |ArrowUnwrap| (Fig.~\ref{fig:ArrowUnwrap}).
% \begin{figure}[h]
\begin{code}
class (Arrow arr) => ArrowUnwrap arr where
	arr a b -> (a -> b)
\end{code}
% \caption{possible ArrowUnwrap typeclass}
% \label{fig:ArrowUnwrap}
% \end{figure}
% We don't do it here
, we avoid doing so for aesthetic reasons. For now, we just implement |ArrowParallel| for normal functions: % (Fig.~\ref{fig:ArrowParallelEdenFns})
% \begin{figure}[h]
\begin{code}
instance (Trans a, Trans b) => ArrowParallel (->) a b (Conf b) where
    parEvalN _ fs as = spawnF fs as
\end{code}
% \caption{ArrowParallel instance for functions in the Eden backend}
% \label{fig:ArrowParallelEdenFns}
% \end{figure}
and the Kleisli type: % (Fig.~\ref{fig:ArrowParallelEdenKleisli}).
% \begin{figure}[h]
\begin{code}
instance (ArrowParallel (->) a (m b) (Conf b),
  Monad m, Trans a, Trans b, Trans (m b)) =>
  ArrowParallel (Kleisli m) a b conf where
    parEvalN conf fs = arr (parEvalN conf (map (\(Kleisli f) -> f) fs)) >>>
                       Kleisli sequence
\end{code}
where |Conf a| is simply defined as |data Conf a = Nil| since Eden does not have a configurable |spawnF| variant.
% \caption{ArrowParallel instance for the Kleisli type in the Eden backend}
% \label{fig:ArrowParallelEdenKleisli}
% \end{figure}

\subsubsection{Default configuration instances}
While the configurability in the instances of the |ArrowParallel| instances above might be nice to have, users probably would prefer a proper default configuration for many parallel programs. These can easily be defined as well as we can see by the example of the default implementation of |ArrowParallel| for the GpH backend:

\begin{code}
instance (NFData b, ArrowChoice arr, ArrowParallel arr a b (Conf b)) =>
  ArrowParallel arr a b () where
    parEvalN _ fs = parEvalN (defaultConf fs) fs

defaultConf :: (NFData b) => [arr a b] -> Conf b
defaultConf fs = stratToConf fs rdeepseq

stratToConf :: [arr a b] -> Strategy b -> Conf b
stratToConf _ strat = Conf strat
\end{code}

The other backends have similarly structured implementations. Note that by restricting the original implementations of our backends to their specific |Conf| type, we also get interoperability between backends for free. We can parallelize one part of a program using one backend, and parallelize the next with another one. We can however only have one instance of |ArrowParallel arr a b ()| present at a time.

\mbcomment{For convenience purposes, ArrowParallel arr a b () is defined as...}
\mbcomment{Until now we described the arrow operations more literally, but in the following sectioins we only talk about the dataflow in general as the idea should be clear now.}

%\FloatBarrier



%%% Local Variables:
%%% mode: latex
%%% TeX-master: "main"
%%% End:
