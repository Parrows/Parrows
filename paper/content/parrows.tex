\section{Parallel Arrows}
\label{sec:parallel-arrows}
We have seen what Arrows are and how they can be used as a general interface to computation. In the following section we will discuss how Arrows constitute a general interface not only to computation, but to \textit{parallel computation} as well. We start by introducing the interface and explaining the reasonings behind it. Then, we discuss some implementations using exisiting parallel Haskells. Finally, we explain why using Arrows for expressing parallelism is beneficial.
\subsection{The ArrowParallel typeclass}
As we have seen earlier, in its purest form, parallel computation (on functions) can be seen as the execution of some functions |a -> b| in parallel, |parEvalN| (Chap.~\ref{sec:parEvalNIntro}).
Translating this into arrow terms gives us a new operator |parEvalN| that lifts a list of arrows |[arr a b]| to a parallel arrow |arr [a] [b]|. % (Fig.~\ref{fig:parEvalNArrowFn}) (
This combinator is similar to our utility function |listApp| from Appendix~\ref{utilfns}, but does parallel instead of serial evaluation.
% \begin{figure}[h]
\begin{code}
parEvalN :: (Arrow arr) => [arr a b] -> arr [a] [b]
\end{code}
% \caption{parEvalN Arrow combinator as a function}
% \label{fig:parEvalNArrowFn}
% \end{figure}
With this definition of |parEvalN|, parallel execution is yet another arrow combinator. But as the implementation may differ depending on the actual type of the arrow |arr| and we want this to be an interface for different backends, we introduce a new typeclass |ArrowParallel arr a b|: %(Fig.~\ref{fig:parEvalNArrowTypeClass1}).
% \begin{figure}[h]
\begin{code}
class Arrow arr => ArrowParallel arr a b where
	parEvalN :: [arr a b] -> arr [a] [b]
\end{code}
% \caption{parEvalN Arrow combinator in a first version of the ArrowParallel typeclass}
% \label{fig:parEvalNArrowTypeClass1}
% \end{figure}
Sometimes parallel Haskells require or allow for additional configuration parameters, \eg an information about the execution environment or the level of evaluation (weak head normal form vs. normal form). For this reason we also introduce an additional |conf| parameter to the function. We also do not want |conf| to be a fixed type, as the configuration parameters can differ for different instances of |ArrowParallel|. So we add it to the type signature of the typeclass as well: %(Fig.~\ref{fig:parEvalNArrowTypeClassFinal}).
% \begin{figure}[h]
\olcomment{|ArrowParallel arr a b conf| or |ArrowParallel conf arr a b|?} \mbcomment{does it really matter?}
\begin{code}
class Arrow arr => ArrowParallel arr a b conf where
	parEvalN :: conf -> [arr a b] -> arr [a] [b]
\end{code}
% \caption{parEvalN Arrow combinator in the final version of the ArrowParallel typeclass}
% \label{fig:parEvalNArrowTypeClassFinal}
% \end{figure}
Note that we don't require the |conf| parameter in every implementation. If it is not needed, we usually just default the |conf| type parameter to |()| and even blank it out in the parameter list of the implemented |parEvalN|, as we will see in the implementation of the Multicore Haskell and the |Par| Monad backends.

\subsection{ArrowParallel instances}

\subsubsection{Multicore Haskell} \label{sec:parrows:multicore}
The Multicore Haskell implementation of |ArrowParallel| is implemented in a straightforward manner by using |listApp| (Appendix~\ref{utilfns}) combined with the |withStrategy :: Strategy a -> a -> a| and |pseq :: a -> b -> b| combinators from Multicore Haskell, where |withStrategy| is the same as |using :: a -> Strategy a -> a| but with flipped parameters.
\begin{figure}[t]
\begin{code}
instance (NFData b, ArrowApply arr, ArrowChoice arr) => ArrowParallel arr a b () where
    	parEvalN _ fs =
       		listApp fs >>>
        	arr (withStrategy (parList rdeepseq)) &&& arr id >>>
        	arr (uncurry pseq)
\end{code}% $ %% formatting
\caption{Fully evaluating ArrowParallel instance for the Multicore Haskell backend.}
\label{fig:ArrowParallelMulticoreRdeepseq}
\end{figure}
For most cases a fully evaluating version like in Fig.~\ref{fig:ArrowParallelMulticoreRdeepseq} would probably suffice, but as the Multicore Haskell interface allows the user to specify the level of evaluation to be done via the |Strategy| interface, our DSL should allow for this. We therefore introduce the |Conf a| data-type that simply wraps a |Strategy a|: 
% \begin{figure}[h]
\begin{code}
data Conf a = Conf (Strategy a)
\end{code}
% \caption{Definition of Conf a}
% \label{fig:confa}
% \end{figure}
We can't directly use the |Strategy a| type here as GHC (at least currently) does not allow type synonyms in type class instances. To get our configurable |ArrowParallel| instance, we simply unwrap the strategy and pass it to |parList| like in the fully evaluating version (Fig.~\ref{fig:ArrowParallelMulticoreConfigurable}).
\begin{figure}[h]
\begin{code}
instance (NFData b, ArrowApply arr, ArrowChoice arr) =>
	ArrowParallel arr a b (Conf b) where
    	parEvalN (Conf strat) fs =
        	listApp fs >>>
        	arr (withStrategy (parList strat)) &&& arr id >>>
        	arr (uncurry pseq)
\end{code}
\caption{Configurable ArrowParallel instance for the Multicore Haskell backend.}
\label{fig:ArrowParallelMulticoreConfigurable}
\end{figure}
\subsubsection{|Par| Monad}
\olcomment{introduce a newcommand for par-monad, "arrows", "parrows" and replace all mentions to them to ensure uniform typesetting}
The |Par| monad implementation (Fig.~\ref{fig:ArrowParallelParMonad}) makes use of Haskells laziness and |Par| monad's |spawnP :: NFData a => a -> Par (IVar a)| function. The latter forks away the computation of a value and returns an |IVar| containing the result in the |Par| monad.


We therefore apply each function to its corresponding input value with || and then fork the computation away with |arr spawnP| inside a |zipWithArr| (Fig.~\ref{fig:zipWithArr}) call. This yields a list |[Par (IVar b)]|, which we then convert into |Par [IVar b]| with |arr sequenceA|. In order to wait for the computation to finish, we map over the |IVar|s inside the |Par| monad with |arr (>>= mapM get)|. The result of this operation is a |Par [b]| from which we can finally remove the monad again by running |arr runPar| to get our output of |[b]|.
\begin{figure}[h]
\begin{code}
instance (NFData b, ArrowApply arr, ArrowChoice arr) =>
	ArrowParallel arr a b conf where
		parEvalN _ fs = 
			(arr $ \as -> (fs, as)) >>>
			zipWithArr (app >>> arr spawnP) >>>
			arr sequenceA >>>
			arr (>>= mapM get) >>>
			arr runPar
\end{code} %$ %% formatting
\caption{ArrowParallel instance for the Par Monad backend.}
\label{fig:ArrowParallelParMonad}
\end{figure}

\subsubsection{Eden}
For both the Multicore Haskell and |Par| Monad implementations we could use general instances of |ArrowParallel| that just require the |ArrowApply| and |ArrowChoice| typeclasses. With Eden this is not the case as we can only spawn a list of functions and we cannot extract simple functions out of arrows. While we could still manage to have only one class in the module by introducing a typeclass: % |ArrowUnwrap| (Fig.~\ref{fig:ArrowUnwrap}).
% \begin{figure}[h]
\begin{code}
class (Arrow arr) => ArrowUnwrap arr where
	arr a b -> (a -> b)
\end{code}
% \caption{possible ArrowUnwrap typeclass}
% \label{fig:ArrowUnwrap}
% \end{figure}
We don't do it here for aesthetic resons, though. For now, we just implement |ArrowParallel| for normal functions: % (Fig.~\ref{fig:ArrowParallelEdenFns})
% \begin{figure}[h]
\begin{code}
instance (Trans a, Trans b) => ArrowParallel (->) a b conf where
parEvalN _ fs as = spawnF fs as
\end{code}
% \caption{ArrowParallel instance for functions in the Eden backend}
% \label{fig:ArrowParallelEdenFns}
% \end{figure}
and the Kleisli type: % (Fig.~\ref{fig:ArrowParallelEdenKleisli}).
% \begin{figure}[h]
\begin{code}
instance (Monad m, Trans a, Trans b, Trans (m b)) =>
	ArrowParallel (Kleisli m) a b conf where
parEvalN conf fs =
	(arr $ parEvalN conf (map (\(Kleisli f) -> f) fs)) >>>
	(Kleisli $ sequence)
\end{code}
% \caption{ArrowParallel instance for the Kleisli type in the Eden backend}
% \label{fig:ArrowParallelEdenKleisli}
% \end{figure}

%\FloatBarrier



%%% Local Variables:
%%% mode: latex
%%% TeX-master: "main"
%%% End:
