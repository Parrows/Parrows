\section{Parallel Arrows}
\label{sec:parallel-arrows}
While Arrows are a general interface to computation, we introduce here specialised Arrows as a general interface to \textit{parallel computations}. We present the |ArrowParallel| type class and explain the reasoning behind it before discussing some parallel Haskell implementations and basic extensions.
\subsection{The ArrowParallel type class}
A~parallel computation (on functions) can be seen as execution of some functions |a -> b| in parallel, as our |parEvalN| prototype shows (Section~\ref{sec:parEvalNIntro}).
Translating this into Arrow terms gives us a new operator |parEvalN| that lifts a list of Arrows |[arr a b]| to a parallel Arrow |arr [a] [b]|. % (Fig.~\ref{fig:parEvalNArrowFn}) (
This combinator is similar to |evalN| from Appendix~\ref{utilfns}, but does parallel instead of serial evaluation.
% \begin{figure}[h]
\begin{code}
parEvalN :: (Arrow arr) => [arr a b] -> arr [a] [b]
\end{code}
% \caption{parEvalN Arrow combinator as a function}
% \label{fig:parEvalNArrowFn}
% \end{figure}
With this definition of |parEvalN|, parallel execution is yet another Arrow combinator. But as the implementation may differ depending on the actual type of the Arrow |arr| - or even the input |a| and output |b| - and we want this to be an interface for different backends, we introduce a new type class |ArrowParallel arr a b|: %(Fig.~\ref{fig:parEvalNArrowTypeClass1}).
% \begin{figure}[h]
\begin{code}
class Arrow arr => ArrowParallel arr a b where
	parEvalN :: [arr a b] -> arr [a] [b]
\end{code}
% \caption{parEvalN Arrow combinator in a first version of the ArrowParallel typeclass}
% \label{fig:parEvalNArrowTypeClass1}
% \end{figure}
Sometimes parallel Haskells require or allow for additional configuration parameters, \eg an information about the execution environment or the level of evaluation (weak head normal form vs. normal form). For this reason we introduce an additional |conf| parameter as we do not want |conf| to be a fixed type, as the configuration parameters can differ for different instances of |ArrowParallel|. % So we add it to the type signature of the typeclass as well: %(Fig.~\ref{fig:parEvalNArrowTypeClassFinal}).
% % \begin{figure}[h]
% \olcomment{|ArrowParallel arr a b conf| or |ArrowParallel conf arr a b|?} \mbcomment{does it really matter?}
\begin{code}
class Arrow arr => ArrowParallel arr a b conf where
	parEvalN :: conf -> [arr a b] -> arr [a] [b]
\end{code}
By restricting the implementations of our backends to a specific |conf| type, we also get interoperability between backends for free. We can parallelize one part of a program using one backend, and parallelize the next with another one.
% \caption{parEvalN Arrow combinator in the final version of the ArrowParallel typeclass}
% \label{fig:parEvalNArrowTypeClassFinal}
% \end{figure}
% , as we will see in the implementation of the GpH Haskell and the |Par| Monad backends.

\subsection{ArrowParallel instances}
With the type class defined, we will now give implementations of it with GpH, the |Par| Monad and Eden.
\subsubsection{Glasgow parallel Haskell} \label{sec:parrows:multicore}
The GpH implementation of |ArrowParallel| is implemented in a straightforward manner in Fig.~\ref{fig:ArrowParallelMulticore}, but a bit different compared to the variant from Section \ref{sec:GpHIntro}. We use |evalN :: [arr a b] -> arr [a] [b]| (definition in Appendix~\ref{utilfns}, think |zipWith ($)| on Arrows) combined with |withStrategy :: Strategy a -> a -> a| from GpH, where |withStrategy| is the same as |using :: a -> Strategy a -> a|, but with flipped parameters. Our |Conf a| datatype simply wraps a |Strategy a|, but could be extended in future versions of our DSL.
\begin{figure}[t]
\begin{code}
data Conf a = Conf (Strategy a)

instance (ArrowChoice arr) =>
  ArrowParallel arr a b (Conf b) where
    parEvalN (Conf strat) fs =
        evalN fs >>>
        arr (withStrategy (parList strat))
\end{code}% $ %% formatting
\caption{GpH |ArrowParallel| instance.}
\label{fig:ArrowParallelMulticore}
\end{figure}

\subsubsection{|Par| Monad}
\olcomment{introduce a newcommand for par-monad, "Arrows", "parrows" and replace all mentions to them to ensure uniform typesetting \done, we write Arrows. also "Monad"? \done}
As for GpH we can easily lift the definition of |parEvalN| for the |Par| Monad to Arrows in Fig.~\ref{fig:ArrowParallelParMonad}. To start off, we define the |Strategy a| and |Conf a| type so we can have a configurable instance of ArrowParallel:
\begin{code}
type Strategy a = a -> Par (IVar a)
data Conf a = Conf (Strategy a)
\end{code}
Now we can once again define our |ArrowParallel| instance as follows: First, we convert our Arrows |[arr a b]| with |evalN (map (>>> arr strat) fs)| into an Arrow |arr [a] [(Par (IVar b))]| that yields composable computations in the |Par| monad. By combining the result of this Arrow with |arr sequenceA|, we get an Arrow |arr [a] (Par [IVar b])|. Then, in order to fetch the results of the different threads, we map over the |IVar|s inside the |Par| Monad with |arr (>>= mapM get)| - our intermediary Arrow is of type |arr [a] (Par [b])|. Finally, we execute the computation |Par [b]| by composing with |arr runPar| and get the final Arrow |arr [a] [b]|.
\begin{figure}[h]
\begin{code}
instance (ArrowChoice arr) => ArrowParallel arr a b (Conf b) where
    parEvalN (Conf strat) fs = evalN (map (>>> arr strat) fs) >>>
                    arr sequenceA >>>
                    arr (>>= mapM Control.Monad.Par.get) >>>
                    arr runPar
\end{code} %$ %% formatting
\caption{|Par| Monad |ArrowParallel| instance.}
\label{fig:ArrowParallelParMonad}
\end{figure}

\subsubsection{Eden}
For both the GpH Haskell and |Par| Monad implementations we could use general instances of |ArrowParallel| that just require the |ArrowChoice| type class. With Eden this is not the case as we can only spawn a list of functions, which we cannot extract from general Arrows. While we could still manage to have only one instance in the module by introducing a type class % |ArrowUnwrap| (Fig.~\ref{fig:ArrowUnwrap}).
% \begin{figure}[h]
\begin{code}
class (Arrow arr) => ArrowUnwrap arr where
	arr a b -> (a -> b)
\end{code}
% \caption{possible ArrowUnwrap typeclass}
% \label{fig:ArrowUnwrap}
% \end{figure}
% We don't do it here
we avoid doing so for aesthetic reasons. For now, we just implement |ArrowParallel| for normal functions and the Kleisli type in Fig.~\ref{fig:ArrowParallelEden}, where |Conf| is simply defined as |data Conf = Nil| since Eden does not have a configurable |spawnF| variant. % (Fig.~\ref{fig:ArrowParallelEdenFns})
\begin{figure}[t]
\begin{code}
instance (Trans a, Trans b) => ArrowParallel (->) a b Conf where
    parEvalN _ = spawnF

instance (ArrowParallel (->) a (m b) Conf,
  Monad m, Trans a, Trans b, Trans (m b)) =>
  ArrowParallel (Kleisli m) a b conf where
    parEvalN conf fs = arr (parEvalN conf (map (\(Kleisli f) -> f) fs)) >>>
                       Kleisli sequence
\end{code} %$ %% formatting
\caption{Eden |ArrowParallel| instance.}
\label{fig:ArrowParallelEden}
\end{figure}

\subsubsection{Default configuration instances}
While the configurability in the instances of the |ArrowParallel| instances above is nice, users probably would like to have proper default configurations for many parallel programs as well. These can also easily be defined as we can see by the example of the default implementation of |ArrowParallel| for the GpH backend:

\begin{code}
instance (NFData b, ArrowChoice arr, ArrowParallel arr a b (Conf b)) =>
  ArrowParallel arr a b () where
    parEvalN _ fs = parEvalN (defaultConf fs) fs

defaultConf :: (NFData b) => [arr a b] -> Conf b
defaultConf fs = stratToConf fs rdeepseq

stratToConf :: [arr a b] -> Strategy b -> Conf b
stratToConf _ strat = Conf strat
\end{code}

The other backends have similarly structured implementations which we do not discuss here for the sake of brevity. We can, however, only have one instance of |ArrowParallel arr a b ()| present at a time, which should not be a problem, though.

Up until now we discussed Arrow operations more in detail, but in the following sections we focus more on the data-flow between the Arrows, now that we have seen that Arrows are capable of expressing parallelism. We do explain new concepts with more details if required for better understanding, though.