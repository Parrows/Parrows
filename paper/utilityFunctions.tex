\section{Utility Functions}\label{utilfns}
To be able to go into detail on parallel arrows, we introduce some utility combinators first, that will help us later: \code{map}, \code{foldl} and \code{zipWith} on arrows.

The \code{mapArr} combinator (Fig.~\ref{fig:mapArr}) lifts any arrow \code{arr a b} to an arrow \code{arr [a] [b]} \cite{programming_with_arrows}:
\begin{figure}[h]
\begin{lstlisting}[frame=htrbl]
mapArr :: ArrowChoice arr => arr a b -> arr [a] [b]
mapArr f =
	arr listcase >>>
	arr (const []) ||| (f *** mapArr f >>> arr (uncurry (:)))
	where listcase [] = Left ()
	      listcase (x:xs) = Right (x,xs)
\end{lstlisting}
\caption{\code{map} over arrows}
\label{fig:mapArr}
\end{figure}
Similarly, we can also define \code{foldlArr} (Fig.~\ref{fig:foldlArr}) that lifts any arrow \code{arr (b, a) b} with a neutral element \code{b} to \code{arr [a] b}:
\begin{figure}[h]
\begin{lstlisting}[frame=htrbl]
foldlArr :: (ArrowChoice arr, ArrowApply arr) => arr (b, a) b -> b -> arr [a] b
foldlArr f b =
	arr listcase >>>
	arr (const b) |||
		(first (arr (\a -> (b, a)) >>> f >>> arr (foldlArr f)) >>> app)
	where listcase [] = Left []
	      listcase (x:xs) = Right (x,xs)
\end{lstlisting}
\caption{\code{foldl} over arrows}
\label{fig:foldlArr}
\end{figure}
Finally, with the help of \code{mapArr} (Fig.~\ref{fig:mapArr}), we can define \lstinline{zipWithArr} (Fig.~\ref{fig:zipWithArr}) that lifts any arrow \code{arr (a, b) c} to an arrow \code{arr ([a], [b]) [c]}.
\begin{figure}[h]
\begin{lstlisting}[frame=htrbl]
zipWithArr :: ArrowChoice arr => arr (a, b) c -> arr ([a], [b]) [c]
zipWithArr f = (arr $ \(as, bs) -> zipWith (,) as bs) >>> mapArr f
\end{lstlisting}
\caption{\code{zipWith} over arrows}
\label{fig:zipWithArr}
\end{figure}
 %$ %% formatting
These combinators make use of the \code{ArrowChoice} type class which provides the \code{(|||)} combinator. It takes two arrows \code{arr a c} and \code{arr b c} and combines them into a new arrow \code{arr (Either a b) c} which pipes all \code{Left a}'s to the first arrow and all \code{Right b}'s to the second arrow.
\begin{figure}[h]
\begin{lstlisting}[frame=htrbl]
(|||) :: ArrowChoice arr a c -> arr b c -> arr (Either a b) c
\end{lstlisting}
\caption{Type signature of \code{(|||)}}
\label{fig:codeSig|||}
\end{figure}

With the zipWithArr combinator we can also write a combinator \code{listApp} (Fig.~\ref{fig:listApp}), that lifts a list of arrows \code{[arr a b]} to an arrow \code{arr [a] [b]}.
\begin{figure}[h]
\begin{lstlisting}[frame=htrbl]
listApp :: (ArrowChoice arr, ArrowApply arr) => [arr a b] -> arr [a] [b]
listApp fs = (arr $ \as -> (fs, as)) >>> zipWithArr app
\end{lstlisting}
\caption{Definition of \code{listApp}}
\label{fig:listApp}
\end{figure}
% $ %% formatting
. Note that  this additionally makes use of the \code{ArrowApply} typeclass that allows us to evaluate arrows with \code{app :: arr (arr a b, a) c}.

% $ %% formatting


%%% Local Variables:
%%% mode: latex
%%% TeX-master: "main"
%%% End:
